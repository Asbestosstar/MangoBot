buildscript {
    repositories {
        mavenLocal()
        mavenCentral()
        jcenter()
        maven {
            url = uri("https://plugins.gradle.org/m2/")
        }
    }

    dependencies {
        classpath 'gradle.plugin.com.github.johnrengelman:shadow:7.1.2'
    }
}

apply plugin: 'com.github.johnrengelman.shadow'
apply plugin: 'java'


group 'org.mangorage'
version "${BOT_VERSION}"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(17)
    }
}

repositories {
    mavenCentral()
    maven {
        url 'https://m2.dv8tion.net/releases'
    }
    maven {
        url 'https://jitpack.io'
    }
}

sourceSets {
    main {
        java {
            srcDir 'java'
        }
        resources {
            srcDir 'resources'
        }
    }
}

dependencies {
    // Libs for Bot
    implementation("net.dv8tion:JDA:$JDA_VERSION") // JDA
    implementation("com.github.walkyst:lavaplayer-fork:$LAVA_VERSION")
    // LavaPlayer to stream audio from YT/FILES to Discord

    implementation("com.google.code.gson:gson:$GSON_VERSION")
    implementation("org.apache.logging.log4j:log4j-core:$LOG4J_VERSION")

    implementation("org.eclipse.mylyn.github:org.eclipse.egit.github.core:2.1.5") // GITHUB API

    implementation project(path: ':mangobotapi')
    implementation project(path: ':mboteventbus')

    runtimeOnly("org.apache.logging.log4j:log4j-slf4j-impl:$LOG4J_VERSION")
}


tasks.register('runBot', JavaExec) {
    group "bot tasks"
    description "Runs the bot"
    args "-dev"

    classpath = sourceSets.main.runtimeClasspath
    mainClass = 'org.mangorage.mangobot.Main'
}

tasks.register('runTestBot', JavaExec) {
    group "bot tasks"
    description "Runs the test bot"
    args "-dev"

    classpath = sourceSets.test.runtimeClasspath
    mainClass = 'org.mangorage.mangobottest.Main'
}

tasks.register('buildBotJar', Jar) {
    group "bot tasks"
    description "Build the Bot Jar with API included"


    duplicatesStrategy = DuplicatesStrategy.EXCLUDE
    from sourceSets.main.output
    from sourceSets.main.resources

    manifest {
        attributes 'Main-Class': 'org.mangorage.mangobot.Main'
    }
}

test {
    useJUnitPlatform()
}


jar {
    duplicatesStrategy = DuplicatesStrategy.EXCLUDE

    manifest {
        attributes 'Main-Class': 'org.mangorage.mangobot.Main'
    }
}
